# error if its not the main module in repository
export 0.8.8

import test3
    coolteen: 'Tim2'

import <stdio.h>

func test2_func[ i:i32 ] -> i64
    return i + 1

##
app test2
    func init [] -> none
        test2_func 1
##

# no assigning external module vars, same as their internal rules
debug:   bool [ coolteen == 'Tim' ]
debug2:         coolteen == 'Tim'


enum Color [ u8 ]
    red:    1
    green:  2
    blue:   3
    alpha:  4


struct Vec2
    public x: f32
    y: f32

    operator + [ b: Vec2 ] -> Vec2
        return Vec2 [ x: x + b.x, y: y + b.y ]

    operator == [ b: Vec2   ] -> bool [ x == b.x && y == b.y ]
    operator /  [ b: double ] -> Vec2 [ x: x / b, y: y / b ]
    operator *  [ b: double ] -> Vec2 [ x: x * b, y: y * b ]

    cast -> string ['{x}, {y}']


class Counter
    public count: i64
    public name:  string

    func init []
        count = 0

    func increment []
        count += 1

    operator += [n: i64] -> i64
        return add[n]

    func add [n: i64] -> i64
        count += n
        return count

    cast -> i64
        return count

class classy_class
    public i: i64

Counter DoubleCounter
    context contextual:classy_class # these we cannot have initializers for, since they must be given
    func increment [] -> none
        super.increment
        count += 1
        count += contextual.i


func math_test[] -> bool
    a: 10 + 5 * 2
    b: a  / 5
    c: a  % 7
    d: 1 << 3
    e: d >> 1
    f: d  & 12
    g: 1  | 2
    h: 5  ^ 3
    return a == 20 && b == 4 && c == 6 && d == 8 && 
           e == 4  && f == 8 && g == 3 && h == 6


func control_flow_test[] -> bool
    total: i64 [ 0 ]

    # for with init::cond::step
    for [ i: i32 0 :: i < 10 :: i += 1 ]
        total += 1

    if [ total != 10 ]
        return false

    # if / el / el
    x:      i64 5
    result: i64 0
    if [ x > 10 ]
        result = 1
    el [ x > 3 ]
        result = 2
    el
        result = 3

    return true

func compound_test[] -> bool
    return control_flow_test && math_test

app test2
    func init [] -> none
        test2_func 1
        if [ not compound_test ]
            puts "FAIL"
